apply plugin: 'maven-publish'

java {
    withSourcesJar()
    withJavadocJar()
}

def githubUrl = 'https://github.com/DanielLiu1123/springdoc-bridge'

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            // see https://docs.gradle.org/current/userguide/publishing_maven.html
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                url = "${githubUrl}"
                name = project.name
                description = project.description ?: project.name
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/license/mit'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'Freeman'
                        name = 'Freeman Liu'
                        email = 'freemanlau1228@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:git://${githubUrl.substring(8)}.git"
                    developerConnection = "scm:git:ssh@${githubUrl.substring(8)}.git"
                    url = "${githubUrl}"
                }
                withXml {
                    def dependenciesNode = asNode().dependencies[0]
                    if (dependenciesNode) {
                        // Add optional configuration dependencies as optional
                        project.configurations.optional.allDependencies.each { dep ->
                            if (dep.group && dep.name && dep.version) {
                                def dependencyNode = dependenciesNode.appendNode('dependency')
                                dependencyNode.appendNode('groupId', dep.group)
                                dependencyNode.appendNode('artifactId', dep.name)
                                dependencyNode.appendNode('version', dep.version)
                                dependencyNode.appendNode('scope', 'compile')
                                dependencyNode.appendNode('optional', 'true')
                            }
                        }
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = "${rootDir}/build/staging-deploy"
        }
    }
}
